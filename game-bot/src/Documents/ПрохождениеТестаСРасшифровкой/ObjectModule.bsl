
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВопросТестаСРасшифровкой.Ссылка КАК Вопрос
		|ИЗ
		|	Справочник.ВопросыТестаСРасшифровкой КАК ВопросТестаСРасшифровкой
		|ГДЕ
		|	ВопросТестаСРасшифровкой.Активность
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВопросТестаСРасшифровкой.Код";
	
	Вопросы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИтогоБаллов = Вопросы.Итог("Баллы");
	
	Если ЗначениеЗаполнено(Вопросы[Вопросы.Количество() - 1].Ответ) Тогда
		ПодвестиРезультат();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
Процедура ПодвестиРезультат()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасшифровкаТестаСРасшифровкой.Ссылка
		|ИЗ
		|	Справочник.РасшифровкиТестаСРасшифровкой КАК РасшифровкаТестаСРасшифровкой
		|ГДЕ
		|	&ИтогоБаллов МЕЖДУ РасшифровкаТестаСРасшифровкой.НачалоДиапазона 
		|		И РасшифровкаТестаСРасшифровкой.КонецДиапазона";
		
	Запрос.УстановитьПараметр("ИтогоБаллов", ИтогоБаллов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 1 Тогда
		ВызватьИсключение СтрШаблон(Нстр("ru = 'Для суммы баллов %1 найдено более одного результата'"));
	КонецЕсли;
	
	Если Выборка.Количество() = 0 Тогда
		ВызватьИсключение СтрШаблон(Нстр("ru = 'Для суммы баллов %1 не найдено результата'"));
	КонецЕсли;
	
	Выборка.Следующий();
	Результат = Выборка.Ссылка;
	
КонецПроцедуры
	
#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

